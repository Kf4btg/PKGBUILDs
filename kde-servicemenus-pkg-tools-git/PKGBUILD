# Maintainer: udeved <udeved@openrc4arch.site40.net>
# Kf4btg@github

pkgname=kde-servicemenus-pkg-tools-git
pkgver=0.r87.baf2160
pkgrel=1
pkgdesc="Kde servicemenu for makepkg, pkgfile, pactree, repo-add & repo-remove, namcap & aur upload."
arch=('any')
url="https://github.com/udeved/kde-servicemenus-pkg-tools"
license=('GPL2')
depends=('kdebase-kdialog' 'namcap' 'graphviz' 'pkgfile' 'sudo')
makedepends=('git')
conflicts=('kde-servicemenus-pkg-tools')
install=pkg-tools.install
source=("$pkgname"::'git://github.com/udeved/kde-servicemenu-pkg-tools.git')
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/$pkgname"
	printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
	cd "$pkgname"

	install -Dm755 bin/pkg-magic $pkgdir/usr/bin/pkg-magic

	for f in ServiceMenus/*;do
		install -Dm644  $f $pkgdir/usr/share/kde4/services/$f
	done
	
	#remove these two desktop files to use in Manjaro
	rm $pkgdir/usr/share/kde4/services/{pkgfile,aur-upload}.desktop

	for f in mime/application/*;do
		install -Dm644 $f $pkgdir/usr/share/$f
	done
	for f in mime/packages/*;do
		install -Dm644 $f $pkgdir/usr/share/$f
	done
}
