# Maintainer: prettyvanilla <prettyvanilla@posteo.at>
# Contributor: Dany Martineau <dany.luc.martineau@gmail.com>

pkgname=klook-git
pkgver=v2.0_10_5_gf01e033
pkgrel=1
pkgdesc='A quick file content preview application that supports integration with Dolphin'
arch=('i686' 'x86_64')
url="https://abf.rosalinux.ru/uxteam/klook-devel"
license=('GPL')
depends=('kdebase-runtime')
makedepends=('git' 'cmake' 'automoc4')
install=$pkgname.install

_gitname="klook-devel"
source=("git+https://abf.rosalinux.ru/uxteam/${_gitname}.git"
        "text-files.patch")
md5sums=('SKIP'
         '5a42d441164a3728f1580b8a5e000944')

pkgver() {
  cd "${_gitname}"
  echo $(git describe --tags | sed 's/-/_/g')
}

build() {
  cd "${_gitname}"
  
  patch -p1 < "${srcdir}/text-files.patch"

  cmake -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr

  make
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
}
